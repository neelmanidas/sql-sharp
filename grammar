<query specification> ::= SELECT <select list> <table expression>

<select list> ::= <asterisk> | <derived column> [ { <comma> <derived column> }... ]

<asterisk> ::= *

<derived column> ::= <value expression> [ [ AS ] <identifier> ]

<value expression> ::= <common value expression>

<common value expression> ::= <parenthesized value expression> | <nonparenthesized value expression primary>

<parenthesized value expression> ::= <left paren> <value expression> <right paren>

<left paren> ::= (

<right paren> ::= )

<nonparenthesized value expression primary> ::= <column reference> | <set function type> <left paren> <value expression> <right paren>

<column reference> ::= <identifier>

<identifier> ::= <identifier start> [ <identifier part> ... ]

<identifier start> ::= !! See the Syntax Rules

<identifier part> ::= <identifier start> | <identifier extend>

<identifier extend> ::= !! See the Syntax Rules

<set function type> ::= AVG | MAX | MIN | SUM | COUNT

<comma> ::= ,

<table expression> ::= FROM <identifier> [ WHERE <boolean value expression> ] [ GROUP BY <column reference> [ { <comma> <column reference> }... ] ]

<boolean value expression> ::= <boolean term> | <boolean value expression> OR <boolean term>

<boolean term> ::= <boolean factor> | <boolean term> AND <boolean factor>

<boolean factor> ::= [ NOT ] <boolean primary> [ IS [ NOT ] <truth value> ]

<boolean primary> ::= <row value predicand> <comparison predicate part 2> | <boolean predicand>

<row value predicand> ::= <nonparenthesized value expression primary> | <row value constructor predicand>

<row value constructor predicand> ::= <common value expression> | <boolean predicand> | <explicit row value constructor>

<boolean predicand> ::= <parenthesized boolean value expression> | <nonparenthesized value expression primary>

<parenthesized boolean value expression> ::= <left paren> <boolean value expression> <right paren>

<explicit row value constructor> ::= <left paren> <value expression> <comma> <row value constructor element list> <right paren>

<row value constructor element list> ::= <value expression> [ { <comma> <value expression> }... ]

<comparison predicate part 2> ::= <comp op> <row value predicand>

<comp op> ::= <equals operator> | <not equals operator> | <less than operator> | <greater than operator> | <less than or equals operator> | <greater than or equals operator>

<equals operator> ::= =

<not equals operator> ::= <less than operator> <greater than operator>

<less than operator> ::= <

<greater than operator> ::= >

<less than or equals operator> ::= <less than operator> <equals operator>

<greater than or equals operator> ::= <greater than operator> <equals operator>

<truth value> ::= TRUE | FALSE | UNKNOWN
